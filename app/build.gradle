apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    ndkVersion '16.1.4479499' // stripReleaseDebugSymbols
    defaultConfig {
        applicationId "com.github.axet.bookreader"
        minSdkVersion 14
        targetSdkVersion 31
        versionCode 412
        versionName "1.14.3"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
    signingConfigs {
        release {
            storeFile file(project.findProperty('RELEASE_STORE_FILE') ?: 'none')
            storePassword project.findProperty('RELEASE_STORE_PASSWORD')
            keyAlias project.findProperty('RELEASE_KEY_ALIAS')
            keyPassword project.findProperty('RELEASE_KEY_PASSWORD')
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = org.apache.commons.io.FilenameUtils.getExtension(android.defaultConfig.applicationId) + "-" + (output.getFilter(com.android.build.OutputFile.ABI) ?: "universal") + "-" + android.defaultConfig.versionName + "-" + variant.buildType.name + ".apk"
        }
    }
    bundle {
        abi {
            enableSplit = true
        }
    }
}

configurations { // TODO fix library dependency copy
        resolvableImpl.extendsFrom(implementation)
        resolvableImpl.canBeResolved(true)
}

dependencies {
    testImplementation 'junit:junit:4+'
    implementation 'com.github.axet:android-library:1.35.18' // implementation project(':android-library')
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation ('com.github.axet.fbreader:library:0.1.17') { exclude group: 'org.apache.httpcomponents', module: 'httpclient' }
    implementation 'com.github.axet:djvulibre:0.1.4'
    implementation 'com.github.axet:pdfium:2.0.14'
    implementation 'com.github.axet:k2pdfopt:0.1.6'
    implementation ('com.github.axet:wget:1.7.0') { exclude group: 'org.json', module: 'json' }
}
